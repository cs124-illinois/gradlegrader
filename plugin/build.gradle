buildscript {
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    }
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.31'
}

apply plugin: 'com.github.johnrengelman.shadow'

repositories {
    mavenCentral()
}

configurations {
    shadow
}

dependencies {
    compileOnly gradleApi()
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'org.apache.httpcomponents:httpclient:4.5.8'
    implementation 'org.apache.commons:commons-text:1.6'
    implementation 'org.eclipse.jgit:org.eclipse.jgit:5.0.2.201807311906-r'
    shadow "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    shadow 'com.google.code.gson:gson:2.8.5'
    shadow 'org.apache.httpcomponents:httpclient:4.5.8'
    shadow 'org.apache.commons:commons-text:1.6'
    shadow 'org.eclipse.jgit:org.eclipse.jgit:5.0.2.201807311906-r'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

compileJava {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

// Maven deployment configuration based on:
// https://medium.com/@nmauti/sign-and-publish-on-maven-central-a-project-with-the-new-maven-publish-gradle-plugin-22a72a4bfd4b

apply plugin: 'signing'
apply plugin: 'maven-publish'

task javadocJar(type: Jar) {
    classifier 'javadoc'
    from javadoc
}

task sourceJar(type: Jar) {
    classifier 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

signing {
    sign configurations.archives
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def ossrhUsername = properties.getProperty('ossrh.username')
def ossrhPassword = properties.getProperty('ossrh.password')

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId 'com.github.cs125-illinois'
            artifactId 'gradlegrader'
            version '1.0.0'

            from components.java

            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }

            // create the signed POM artifact
            pom.withXml {
                def pomFile = file("${project.buildDir}/generated-pom.xml")
                writeTo(pomFile)
                def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                artifact(pomAscFile) {
                    classifier = null
                    extension = 'pom.asc'
                }
            }
            // create the signed artifacts
            project.tasks.signArchives.signatureFiles.each {
                artifact(it) {
                    def matcher = it.file =~ /-(sources|javadoc)\.jar\.asc$/
                    if (matcher.find()) {
                        classifier = matcher.group(1)
                    } else {
                        classifier = null
                    }
                    extension = 'jar.asc'
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = DELEGATE_FIRST

            description 'CS 125 autograder'
            name 'GradleGrader'
            url 'https://github.com/cs125-illinois/gradlegrader'
            organization {
                name 'com.github.cs125-illinois'
                url 'https://github.com/cs125-illinois'
            }
            issueManagement {
                system 'GitHub'
                url 'https://github.com/cs125-illinois/gradlegrader/issues'
            }
            licenses {
                license {
                    name 'Apache License 2.0'
                    url 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://github.com/cs125-illinois/gradlegrader'
                connection 'scm:git:git://github.com/cs125-illinois/gradlegrader.git'
                developerConnection 'scm:git:ssh://git@github.com:cs125-illinois/gradlegrader.git'
            }
            developers {
                developer {
                    name 'CS 125'
                }
            }
        }
    }
}

model {
    tasks.publishMavenJavaPublicationToMavenLocal {
        dependsOn project.tasks.signArchives
    }
    tasks.publishMavenJavaPublicationToMavenRepository {
        dependsOn project.tasks.signArchives
    }
}